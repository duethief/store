apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'tomcat'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.2.2'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://oss.sonatype.org/content/repositories/snapshots/"
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

eclipse{
    jdt{
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }
}

dependencies {
    def springVersion = "4.0.2.RELEASE"
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'ch.qos.logback:logback-classic:1.1.1'
    compile 'mysql:mysql-connector-java:5.1.27'
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-context-support:${springVersion}"
    compile "org.springframework:spring-jdbc:${springVersion}"
    compile "org.springframework:spring-orm:${springVersion}"
    compile "org.springframework:spring-tx:${springVersion}"
    compile "org.springframework:spring-web:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"

    compile 'org.hibernate:hibernate-core:4.3.1.Final'
    compile 'org.hibernate:hibernate-entitymanager:4.3.1.Final'
    compile 'org.hibernate:hibernate-validator:5.0.3.Final'
    compile 'com.mysema.querydsl:querydsl-core:3.3.1'
    compile 'com.mysema.querydsl:querydsl-apt:3.3.1'
    compile 'com.mysema.querydsl:querydsl-jpa:3.3.1'
    compile 'com.mysema.querydsl:querydsl-sql:3.3.1'
    compile 'org.hibernate:hibernate-c3p0:4.3.4.Final'
    compile 'org.springframework.data:spring-data-jpa:1.5.0.RELEASE'
    compile 'c3p0:c3p0:0.9.1.2'
    compile 'com.jolbox:bonecp:0.8.0.RELEASE'
    testCompile "junit:junit:4.11"
    groovy "org.codehaus.groovy:groovy-all:2.1.5"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.0-SNAPSHOT"
    testCompile "org.spockframework:spock-spring:1.0-groovy-2.0-SNAPSHOT"

    String tomcatVersion = '7.0.47'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    tomcat "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'javax.websocket:javax.websocket-api:1.0'
    providedCompile 'javax.servlet:jsp-api:2.0'
    providedCompile "org.apache.tomcat:tomcat-servlet-api:${tomcatVersion}"

    compile 'jstl:jstl:1.2'
    compile 'org.apache.velocity:velocity:1.7'
    compile 'org.freemarker:freemarker:2.3.20'
    compile 'org.apache.tiles:tiles-jsp:3.0.3'
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

tomcatRun {
    contextPath = ""
    URIEncoding = 'UTF-8'
    dependsOn war
}

tomcatRunWar {
    dependsOn war
}